INSERT {
    GRAPH <local:inference/sparql/violation.sparql> {
        ?violation_iri
            a :Violation ;
            :code ?code ;
            :plugin ?plugin .

        ?page
            :version ?plugin_version ;
            :violation ?violation_iri .
    }
} WHERE {
    ?page
        a octa:Page ;
        :code ?code ;
        octa:isChildOf+ ?plugin_version .

    ?plugin_version
        a :Flake8PluginVersion ;
        :plugin ?plugin .

    BIND(
        IRI(
            CONCAT(
                "https://flake8.codes/",
                ?code
            )
        )
        AS ?violation_iri
    )
}